buildscript {
    ext.kotlin_version = '1.1.1'

    repositories {
        jcenter()
        mavenCentral()
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:${kotlin_version}"
        classpath 'org.ajoberstar:gradle-git:+'
        classpath 'com.github.jengelman.gradle.plugins:shadow:1.2.4'
    }
}

apply plugin: 'java'
apply plugin: 'kotlin'
apply plugin: 'org.ajoberstar.grgit'
apply plugin: 'org.ajoberstar.release-opinion'
apply plugin: 'com.github.johnrengelman.shadow'

description = 'TNT can destroy other TNT.'
group = 'net.moltendorf.bukkit.likedynomite'

sourceCompatibility = 1.8

def date = new Date().format('YYYY/MM/dd HH:mm:ss')
def author = "${grgit.log(maxCommits: 1).get(0).author.name} <${grgit.log(maxCommits: 1).get(0).author.email}>"
def website = "https://github.com/moltendorf/${project.name}"

tasks.build.mustRunAfter 'clean'

tasks.processResources.dependsOn 'expandTemplates'
tasks.shadowJar.dependsOn 'build'
tasks.release.dependsOn 'clean', 'shadowJar'

task expandTemplates(type: Copy) {
    outputs.upToDateWhen { false }

    from 'src/main/templates'
    into "$buildDir/resources/main"

    expand project: project, author: author, website: website, date: date
}

jar {
    manifest.attributes 'Specification-Title': project.name,
            'Specification-Vendor': author,
            'Implementation-Title': "${project.group}.${project.name}",
            'Implementation-Version': project.version
}

shadowJar {
    baseName = project.name
    version = project.version
    classifier = null
}

repositories {
    mavenCentral()

    maven { url 'https://jitpack.io' }
    maven { url 'https://oss.sonatype.org/content/repositories/snapshots' }
    maven { url 'https://hub.spigotmc.org/nexus/content/groups/public' }
    maven { url 'https://fab.moltendorf.net/nexus' }
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib-jre8:${kotlin_version}"

    compile 'org.spigotmc:spigot-api:1.9+'

    testCompile group: 'junit', name: 'junit', version: '4.12'
}

configurations {
    runtime.exclude module: 'spigot-api'
}
